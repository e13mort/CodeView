/*
 * This file is part of CodeView.
 * Copyright (c) 2020 Pavel Novikov
 *
 * CodeView is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * CodeView is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with CodeView.  If not, see <https://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
plugins {
    id 'java'
    id 'kotlin'
    id 'application'
    id 'com.github.gmazzo.buildconfig' version '1.5.0'
    id 'kotlin-kapt'
}

application {
    mainClassName = 'CliClientKt'
}

applicationName = 'codeview'

sourceCompatibility = 1.8
targetCompatibility = "8"
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation project(':codeview')
    implementation project(':cache:cache-common')
    implementation project(':cache:content-storage')
    implementation project(':cache:cache-backend')
    implementation project(':cache:cache-frontend')
    implementation project(':cache:cached-output')
    implementation project(':log')
    implementation project(':backend:java-backend')
    implementation project(':frontend:pulm-frontend')
    implementation project(':datasource:common')
    implementation project(':datasource:github-datasource')
    implementation project(':datasource:git-datasource')
    implementation project(':utils:github-url-parser')
    implementation project(':output:engine-based-output')
    implementation 'com.github.ajalt:clikt:2.0.0'
    implementation 'com.google.dagger:dagger:2.23.2'
    kapt 'com.google.dagger:dagger-compiler:2.23.2'

    if (JavaVersion.current().isJava9Compatible()) {
        implementation 'javax.annotation:javax.annotation-api:1.3.2'
    }
    
    testImplementation 'org.junit.jupiter:junit-jupiter:5.4.2'
}

buildConfig {
    packageName("com.github.e13mort.codeview.cli")
    buildConfigField('String', 'APP_NAME', "\"$applicationName\"")
    buildConfigField('String', 'DEFAULT_SOURCE_ROOT_PATH', "\".\"")
    buildConfigField('String', 'DEFAULT_OUTPUT_FILE_NAME', "\"output\"")
}
